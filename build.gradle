repositories {
    mavenCentral()
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'net.saliman:gradle-cobertura-plugin:2.2.2'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.3.1'
    }
}

allprojects {
    apply plugin: 'cobertura'
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.7
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile 'junit:junit:4.11'
        testCompile 'org.hamcrest:hamcrest-core:1.3'
        testCompile 'org.hamcrest:hamcrest-library:1.3'
    }

    cobertura {
        coverageIgnoreTrivial = true
    }
}

test.dependsOn(subprojects.test)


// Cobertura ///////

def coverageReports = subprojects.collect { project ->
    new File(project.projectDir, '/build/cobertura/cobertura.ser')
}
cobertura {
    // coveralls plugin depends on xml format report
    coverageFormats = ['html', 'xml']
    coverageMergeDatafiles = coverageReports
}


// Coveralls ///////

apply plugin: 'com.github.kt3k.coveralls'

coveralls {
    sourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs).files.absolutePath
}
